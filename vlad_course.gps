	SIMULATE
	REALLOCATE COM,32720

TTIME  FUNCTION RN1,D2
.3,30/1,35
PKMM   FUNCTION RN1,D2
.6,4/1,2
FKMM   FUNCTION P1,E3
1,2/2,FN$PKMM/3,1
TERM	FUNCTION P1,E3
1,TERM1/2,TERM2/3,TERM3
PRIO FUNCTION P1,E3
1,50/2,45/3,40

	INITIAL X$ENDALL,0
	INITIAL X$CTERM1,0
	INITIAL X$CTERM2,0
	INITIAL X$CTERM3,0
	INITIAL X$END1,0
	INITIAL X$END2,0
	INITIAL X$END3,0

	GENERATE 140,35
	ASSIGN 1,1              флаг о нахождении в ARM 1
TERM1 TEST E P2,1,CON1      проверка прохода ЦВК
	PRIORITY 50             установка приоритета
	SAVEVALUE END1+,1       добавление внутреннего счетчика
	TEST GE X$END1,4,CON1   проверка счетчика на каждую 4-ую заявку
	SAVEVALUE CTERM1+,1     подсчет количества заявок идущих на удаление
	SAVEVALUE END1,0        обнуление внутреннего счетчика
	TRANSFER ,DEL           передача на уничтожение заявки

CON1 ASSIGN 2,0             обнуление флага о прохождении ЦВК
	QUEUE QTERM1
	SEIZE RTERM1
	DEPART QTERM1
	ADVANCE FN$TTIME        время выполнения заявки на терминале 1
	PRIORITY 48             установка приортета
	RELEASE RTERM1

ARM1 QUEUE QARM1
	SEIZE RARM1
	DEPART QARM1
	ADVANCE 20,4            время выполенния заявки на АРМ 1 
	RELEASE RARM1
	PRIORITY 47             установка приортита
    TRANSFER .3,TERM1,KMM   









*45 на цвк



	GENERATE 190,10
	ASSIGN 1,2              флаг о нахождении в терминале 2
TERM2 TEST E P2,1,CON2      проверка прохода ЦВК
    PRIORITY 50             установка приоритета
	SAVEVALUE END2+,1       добавление внутреннего счетчика
	TEST GE X$END2,4,CON2   проверка счетчика на каждую 4-ую заявку 
	SAVEVALUE CTERM2+,1     подсчет количества заявок идущих на удаление
	SAVEVALUE END2,0        обнуление внутреннего счетчика
	TRANSFER ,DEL           передача на уничтожение заявки

CON2 ASSIGN 2,0             обнуление флага о прохождении ЦВК
    QUEUE QTERM2
	SEIZE RTERM2
	DEPART QTERM2
	ADVANCE FN$TTIME        время выполнения заявки на терминале 2
	PRIORITY 48             установка приортета
	RELEASE RTERM2

ARM2 QUEUE QARM2
	SEIZE RARM2
	DEPART QARM2
	ADVANCE 25,5            время выполенния заявки на АРМ 2
	RELEASE RARM2
	PRIORITY 44             установка приортита
    TRANSFER .3,TERM2,KMM









	GENERATE 230,10
	ASSIGN 1,3              флаг о нахождении в ARM 3
TERM3 TEST E P2,1,CON3      проверка прохода ЦВК
    PRIORITY 50             установка приоритета
    SAVEVALUE END3+,1       добавление внутреннего счетчика
	TEST E X$END3,4,CON3    проверка счетчика на каждую 4-ую заявку
	SAVEVALUE CTERM3+,1     подсчет количества заявок идущих на удаление
	SAVEVALUE END3,0        обнуление внутреннего счетчика
	TRANSFER ,DEL           передача на уничтожение заявки

CON3 ASSIGN 2,0             обнуление флага о прохождении ЦВК
    QUEUE QTERM3
	SEIZE RTERM3
	DEPART QTERM3
	ADVANCE FN$TTIME        время выполнения заявки на терминале 3
	PRIORITY 48             установка приоритета
	RELEASE RTERM3
	   
ARM3 QUEUE QARM3
	SEIZE RARM3
	DEPART QARM3
	ADVANCE 28,8            время выполенния заявки на АРМ 3
	RELEASE RARM3           
	PRIORITY 39             установка приоритета
    TRANSFER .3,TERM3,KMM

KMM SPLIT 1,CPY             создание копии транзакта
	LINK CKMM,FIFO          вход в цепь пользователя

CPY ASSIGN 3,1              флаг о том, что транзакт - копия
	TRANSFER ,UNLI

SZ  SEIZE RKMM
    ADVANCE FN$FKMM         
	RELEASE RKMM
	
UNLI UNLINK CKMM,SZ,1       выпусакем тразакты из цепи пользователя
	TEST NE P3,1,DEL        проверка является ли траназкт копией
	TEST E P2,1,CVK         проверка был ли транзанкт на ЦВК
	TRANSFER ,FN$TERM       переход на реминал 

CVK QUEUE QCVK
	SEIZE RCVK
	DEPART QCVK
	ADVANCE 48,7            время выполнения заявки на ЦВК
	RELEASE RCVK
	PRIORITY FN$PRIO        назначение приоритета в соответствии с терм
	ASSIGN 2,1              флаг о прохождении ЦВК
	SAVEVALUE ENDALL+,1     увелечении общего счетчика
	TRANSFER ,KMM           переход на KMM

DEL TERMINATE               удаление заявки

	GENERATE ,,,1           генерация по одному транзакту
	TEST E X$ENDALL,270     проверка на условие окончания моделирования
	TERMINATE 1
	START 1
	END