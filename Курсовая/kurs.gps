	SIMULATE
	REALLOCATE COM,32719

FARM	FUNCTION RN1,D2
.7,23/1,28

TK3	FUNCTION RN1,D2
.9,2/1,1

TERM	FUNCTION P1,E3
1,TERM1/2,TERM2/3,TERM3

TKMM	FUNCTION P1,E3
1,4/2,3/3,FN$TK3

CNTARM	FUNCTION P1,E3
1,ZARM1/2,ZARM2/3,ZARM3

CNTCVK	FUNCTION P1,E3
1,ZCVK1/2,ZCVK2/3,ZCVK3

COUNT1	FUNCTION P2,E2
0,FN$CNTARM/1,FN$CNTCVK

CVKPR	FUNCTION P1,D3
1,9/2,8/3,7

TERMPR	FUNCTION P2,D2
0,30/1,25

	INITIAL X$EXIT,1100
	INITIAL X$ENDOBSL,0 конец обслуживания заявок
	INITIAL X$ZARM1,0
	INITIAL X$ZARM2,0
	INITIAL X$ZARM3,0
	INITIAL X$ZCVK1,0
	INITIAL X$ZCVK2,0
	INITIAL X$ZCVK3,0

	GENERATE 140,35,,27
	ASSIGN 1,1
TERM1	PRIORITY FN$TERMPR
	ASSIGN 2,0
	QUEUE QTERM1
	SEIZE STERM1
	DEPART QTERM1
	ADVANCE 30,10
	RELEASE STERM1
	SAVEVALUE EXIT-,1

ARM1	PRIORITY 20
	QUEUE QARM1
	SEIZE SARM1
	DEPART QARM1
	ADVANCE FN$FARM
	RELEASE SARM1
	TRANSFER .3,TERM1,KMMIN	

	GENERATE 180,,,34
	ASSIGN 1,2
TERM2	PRIORITY FN$TERMPR
	ASSIGN 2,0
	QUEUE QTERM2
	SEIZE STERM2
	DEPART QTERM2
	ADVANCE 30,15
	RELEASE STERM2
	SAVEVALUE EXIT-,1

ARM2	PRIORITY 15
	QUEUE QARM2
	SEIZE SARM2
	DEPART QARM2
	ADVANCE FN$FARM
	RELEASE SARM2
	TRANSFER .3,TERM2,KMMIN

	GENERATE 220,,,25
	ASSIGN 1,3
TERM3	PRIORITY FN$TERMPR
	ASSIGN 2,0
	QUEUE QTERM3
	SEIZE STERM3
	DEPART QTERM3
	ADVANCE 35,12
	RELEASE STERM3
	SAVEVALUE EXIT-,1

ARM3	PRIORITY 10
	QUEUE QARM3
	SEIZE SARM3
	DEPART QARM3
	ADVANCE FN$FARM
	RELEASE SARM3
	TRANSFER .3,TERM3,KMMIN

KMMIN	LINK CUKMM,FIFO,KMM	

KMM	SEIZE SKMM
	ADVANCE FN$TKMM
	RELEASE SKMM
	SAVEVALUE FN$COUNT1+,1
	UNLINK CUKMM,KMM,1
	TEST E P2,0,FN$TERM

	SAVEVALUE ENDOBSL+,1
	TEST E X$ENDOBSL,5,CVK
	SAVEVALUE ENDOBSL,0
	TRANSFER ,DEL

CVK	QUEUE QCVK
	SEIZE SCVK
	DEPART QCVK
	ADVANCE 36,5
	RELEASE SCVK
	PRIORITY FN$CVKPR
	ASSIGN 2,1
	TRANSFER ,KMMIN

DEL	TERMINATE

	GENERATE ,,,1
	TEST E X$EXIT,0
	TERMINATE 1
	START 1

	END
